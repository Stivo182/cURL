#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем КонтекстЯдра; // ВнешняяОбработка
Перем Ожидаем; // ВнешняяОбработка
Перем Curl; // ВнешняяОбработка

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Инициализация.
// 
// Параметры:
//  КонтекстЯдраПараметр - ВнешняяОбработка - Контекст ядра параметр
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	
	НайтиИПодключитьCURL();
		
КонецПроцедуры

// Заполняет набор тестов.
// 
// Параметры:
//  НаборТестов - ВнешняяОбработка - Набор тестов
//  КонтекстЯдраПараметр - ВнешняяОбработка - Контекст ядра параметр
Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_СЗаголовками");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_ДвоичныеДанные");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_Файл");
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_БезТела");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоТекст");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоСтруктура");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоСоответствие");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоДвоичныеДанные");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоФайл");  
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_POST_ТелоДвоичныеДанныеВоВременномХранилище"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_САутентификациейBasic"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_CАутентификациейDigest"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапрос"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_НеПеренаправлятьЗапрос"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапросНаДругойХостБезАутентификации"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапросНаДругойХостСАутентификацией"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_HTTP_PUT"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_HTTP_PUT_Файл"); 
	НаборТестов.Добавить("ТестДолжен_ОтправитьЗапрос_HTTP_PUT_ДвоичныеДанные"); 
	НаборТестов.Добавить("ТестДолжен_ПередатьФайл_HTTP");
	НаборТестов.Добавить("ТестДолжен_ВызовИсключенияПриНеверномПутиКИсполняемомуФайлу"); 
	НаборТестов.Добавить("ТестДолжен_ПроверитьЗаголовкиОтветаМожетСодержатьКлючСНесколькимиЗначениями");	
	НаборТестов.Добавить("ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаЗапросаПоИмениВнеЗависимостиОтРегистра");	
	НаборТестов.Добавить("ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаОтветаПоИмениВнеЗависимостиОтРегистра");	
	НаборТестов.Добавить("ТестДолжен_ПроверитьЧтоКукиОбновленыПослеВыполненияЗапроса");	
	НаборТестов.Добавить("ТестДолжен_ПроверитьЧтоКукиНеОбновленыПослеВыполненияЗапроса");	
	НаборТестов.Добавить("ТестДолжен_ПолучитьФайл_FTP");	
	НаборТестов.Добавить("ТестДолжен_ВыполнитьАвторизацию_FTP");	
	НаборТестов.Добавить("ТестДолжен_ПопытатьсяВыполнитьАвторизациюСНевернымиДанными_FTP");
	НаборТестов.Добавить("ТестДолжен_ПопытатьсяПолучитьНесуществующийФайлИВернутьСостояние550_FTP");
		
	НаборТестов.Добавить("ТестДолжен_ПроверитьНаличиеВКомандеДобавленнойПользовательскойОпции");
	НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеУдаленнойПользовательскойОпции");
	НаборТестов.Добавить("ТестДолжен_ПроверитьОтсутствиеВКомандеОчищенныхПользовательскихОпций");
	
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_АутентификацияAWS3");
	
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_СертификатКлиента");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_СертификатКлиентаСТипом");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_ЗакрытыйКлючСертификата");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_ЗакрытыйКлючСертификатаСТипом");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_ИспользованиеСертификатовУдостоверяющихЦентровИзОС");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_СертификатыУдостоверяющихЦентров");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_НаОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОС");
	НаборТестов.Добавить("ТестДолжен_ПроверитьСтрокуКоманды_НаОтсутствиеСертификатовУдостоверяющихЦентровИзФайла");
		
КонецПроцедуры

// Обработчик события перед запуском теста.
Процедура ПередЗапускомТеста() Экспорт
		
	Curl = ВнешниеОбработки.Создать("cURL");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ТестДолжен_ОтправитьЗапрос_GET() Экспорт
	
	Curl.Get("https://httpbin.org/get");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_СЗаголовками() Экспорт
	
	Агент = "1C:Enterprise";
	Ответ = Curl.УстановитьЗаголовок("User-Agent", Агент)
				.Get("https://httpbin.org/get")
				.ОтветКакJson();
	
	Ожидаем.Что(Ответ["headers"]["User-Agent"]).Равно(Агент);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_ДвоичныеДанные() Экспорт
	
	Размер = 16;
	URL = СтрШаблон("https://httpbin.org/bytes/%1", Формат(Размер, "ЧГ="));
	
	ДвоичныеДанные = Curl.GetBinaryData(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(ДвоичныеДанные.Размер()).Равно(Размер);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_Файл() Экспорт
	
	Размер = 16;
	URL = СтрШаблон("https://httpbin.org/bytes/%1", Формат(Размер, "ЧГ="));
	
	ПутьКФайлу = Curl.GetFile(URL);
	Файл = Новый Файл(ПутьКФайлу);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Файл.Размер()).Равно(Размер);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_БезТела() Экспорт
	
	Curl.Post("https://httpbin.org/post");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоТекст() Экспорт
	
	Данные = СтрШаблон("Name=%1&Id=%2&Cyrillic=%3", 
		"blink", 
		"182", 
		КодироватьСтроку("Йогурт", СпособКодированияСтроки.КодировкаURL));
		
	Ответ = Curl.Post("https://httpbin.org/post", Данные).ОтветКакJson();
	
	ПолученныеПараметры = Ответ["form"];
	
	Ожидаем.Что(ПолученныеПараметры["Name"]).Равно("blink");
	Ожидаем.Что(ПолученныеПараметры["Id"]).Равно("182");
	Ожидаем.Что(ПолученныеПараметры["Cyrillic"]).Равно("Йогурт");
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоСтруктура() Экспорт
	
	Тело = Новый Структура();
	Тело.Вставить("Name", "blink");
	Тело.Вставить("Id", 182);
	Тело.Вставить("Bool", Истина);
	Тело.Вставить("Cyrillic", "Йогурт");
	Тело.Вставить("SpecChars", "p=1&p=2");

	Ответ = Curl.Post("https://httpbin.org/post", Тело).ОтветКакJson();
	
	ПолученныеПараметры = Ответ["form"];
	
	Ожидаем.Что(ПолученныеПараметры["Name"]).Равно("blink");
	Ожидаем.Что(ПолученныеПараметры["Id"]).Равно("182");
	Ожидаем.Что(ПолученныеПараметры["Bool"]).Равно("true");
	Ожидаем.Что(ПолученныеПараметры["Cyrillic"]).Равно("Йогурт");
	Ожидаем.Что(ПолученныеПараметры["SpecChars"]).Равно("p=1&p=2");
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоСоответствие() Экспорт
	
	Тело = Новый Соответствие();
	Тело.Вставить("Name", "blink");
	Тело.Вставить("Id", 182);
	Тело.Вставить("Bool", Истина);
	Тело.Вставить("Cyrillic", "Йогурт");
	Тело.Вставить("SpecChars", "p=1&p=2");

	Ответ = Curl.Post("https://httpbin.org/post", Тело).ОтветКакJson();
	
	ПолученныеПараметры = Ответ["form"];
	
	Ожидаем.Что(ПолученныеПараметры["Name"]).Равно("blink");
	Ожидаем.Что(ПолученныеПараметры["Id"]).Равно("182");
	Ожидаем.Что(ПолученныеПараметры["Bool"]).Равно("true");
	Ожидаем.Что(ПолученныеПараметры["Cyrillic"]).Равно("Йогурт");
	Ожидаем.Что(ПолученныеПараметры["SpecChars"]).Равно("p=1&p=2");
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоДвоичныеДанные() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	Поток = Новый ПотокВПамяти(Буфер); 
	
	Тело = Поток.ЗакрытьИПолучитьДвоичныеДанные();   
	
	Ответ = Curl.Post("https://httpbin.org/post", Тело).ОтветКакJson();
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоФайл() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
  	Файл = Новый Файл(ПутьКФайлу);
	
	Попытка
		Ответ = Curl.Post("https://httpbin.org/post", Файл).ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УдалитьФайлы(ПутьКФайлу);
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_POST_ТелоДвоичныеДанныеВоВременномХранилище() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
  	
  	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Ответ = Curl.Post("https://httpbin.org/post", Адрес).ОтветКакJson();
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_САутентификациейBasic() Экспорт

	ИмяПользователя = "user";
	Пароль = "secret";
	
	URL = СтрШаблон("https://httpbin.org/basic-auth/%1/%2", ИмяПользователя, Пароль);
	Ответ = Curl.Аутентификация(ИмяПользователя, Пароль)
				.Get(URL)
				.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Ответ["authenticated"]).Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_CАутентификациейDigest() Экспорт
	
	ИмяПользователя = "user";
	Пароль = "secret";
	
	URL = СтрШаблон("https://httpbin.org/digest-auth/auth/%1/%2", ИмяПользователя, Пароль);
	Ответ = Curl.АутентификацияDigest("user", "secret")
				.Get(URL)
				.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Ответ["authenticated"]).Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапрос() Экспорт
	
	Location = КодироватьСтроку("https://httpbin.org/status/200", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Curl.ПеренаправлятьЗапрос().Get(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_НеПеренаправлятьЗапрос() Экспорт
	
	Location = КодироватьСтроку("https://httpbin.org/status/200", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Curl.Get(URL);
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(301);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапросНаДругойХостБезАутентификации() Экспорт
	
	Location = КодироватьСтроку("https://postman-echo.com/headers", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);
	
	Ответ = Curl
		.ПеренаправлятьЗапрос()
		.Аутентификация("user", "secret")
		.Get(URL)
		.ОтветКакJson();
		
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Ответ["headers"].Получить("authorization")).ЭтоНеопределено();
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_GET_ПеренаправитьЗапросНаДругойХостСАутентификацией() Экспорт

	Location = КодироватьСтроку("https://postman-echo.com/headers", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/redirect-to?url=%1&status_code=301", Location);

	Ответ = Curl
		.ПеренаправлятьЗапрос(, Истина)
		.Аутентификация("user", "secret")
		.Get(URL)
		.ОтветКакJson();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Ответ["headers"].Получить("authorization")).Не_().ЭтоНеопределено();
		
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_HTTP_PUT() Экспорт
	
	Curl.Put("https://httpbin.org/put");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_HTTP_PUT_Файл() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
  	Файл = Новый Файл(ПутьКФайлу);
	
	Попытка
		Ответ = Curl.Put("https://httpbin.org/put", Файл).ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УдалитьФайлы(ПутьКФайлу);
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ОтправитьЗапрос_HTTP_PUT_ДвоичныеДанные() Экспорт
	
	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	Поток = Новый ПотокВПамяти(Буфер); 
	
	Тело = Поток.ЗакрытьИПолучитьДвоичныеДанные();   
	
	Ответ = Curl.Put("https://httpbin.org/put", Тело).ОтветКакJson();
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ПередатьФайл_HTTP() Экспорт

	РазмерБуфера = 16;
	РазмерБуфераСтрокой = Формат(РазмерБуфера, "ЧГ=");
	
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
  	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(Буфер);
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
	Попытка
		Ответ = Curl.UploadFile("https://httpbin.org/put", ПутьКФайлу).ОтветКакJson();
	Исключение
		УдалитьФайлы(ПутьКФайлу);	
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УдалитьФайлы(ПутьКФайлу);
				
	Ожидаем.Что(Ответ["headers"]["Content-Length"]).Равно(РазмерБуфераСтрокой);
	
КонецПроцедуры

Процедура ТестДолжен_ВызовИсключенияПриНеверномПутиКИсполняемомуФайлу() Экспорт
	
	БылоИсключение = Ложь;
	Попытка
		Curl.УказатьИсполняемыйФайл("ccurl")
			.Get("https://httpbin.org/get");
	Исключение
		БылоИсключение = Истина;
	КонецПопытки;
	
	Ожидаем.Что(БылоИсключение, "Не вызывалось исключение при неверном пути к исполняемому файлу").Равно(Истина);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЗаголовкиОтветаМожетСодержатьКлючСНесколькимиЗначениями() Экспорт
	
	Параметр1 = КодироватьСтроку("name=Jack", СпособКодированияСтроки.КодировкаURL);
	Параметр2 = КодироватьСтроку("age=25", СпособКодированияСтроки.КодировкаURL);
	
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1&set-cookie=%2", Параметр1, Параметр2);

	ЗаголовкиОтвета = Curl.Get(URL).ЗаголовкиОтвета();
	
	ЗначениеЗаголовка = ЗаголовкиОтвета.Получить("set-cookie");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(ЗначениеЗаголовка, "Ожидаем тип Массив").ИмеетТип(Тип("Массив"));
	Ожидаем.Что(ЗначениеЗаголовка.Количество(), "Ожидаем 2 элемента").Равно(2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаЗапросаПоИмениВнеЗависимостиОтРегистра() Экспорт
	
	Эталон = "name=value";
	Curl.УстановитьЗаголовок("COOKIE", Эталон);
	
	Значение = Curl.Заголовок("Cookie");
	
	Ожидаем.Что(Значение).Равно(Эталон);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьВозможностьПолученияЗаголовкаОтветаПоИмениВнеЗависимостиОтРегистра() Экспорт
	
	Эталон = "text/html; charset=utf-8";
	
	Параметр = КодироватьСтроку(Эталон, СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?Content-Type=%1", Параметр);

	Значение = Curl
		.Get(URL)
		.ЗаголовокОтвета("Content-TYPE");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Значение).Равно(Эталон);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоКукиОбновленыПослеВыполненияЗапроса() Экспорт
	
	Параметр = КодироватьСтроку("Age=18", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1", Параметр);

	Куки = Curl
		.УстановитьКуки("Name=Jhon")
		.Get(URL)
		.Куки();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Куки.Получить("Name")).Равно("Jhon");
	Ожидаем.Что(Куки.Получить("Age")).Равно("18");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоКукиНеОбновленыПослеВыполненияЗапроса() Экспорт
	
	Параметр = КодироватьСтроку("Age=18", СпособКодированияСтроки.КодировкаURL);
	URL = СтрШаблон("https://httpbin.org/response-headers?set-cookie=%1", Параметр);

	Куки = Curl
		.УстановитьКуки("Name=Jhon")
		.ОбновлятьКуки(Ложь)
		.Get(URL)
		.Куки();
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(200);
	Ожидаем.Что(Куки.Получить("Name")).Равно("Jhon");
	Ожидаем.Что(Куки.Получить("Age")).ЭтоНеопределено();
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьФайл_FTP() Экспорт
	
	Текст = Curl.GetString("ftp://demo:password@test.rebex.net/readme.txt");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(226);
	Ожидаем.Что(Текст).Заполнено();
	
КонецПроцедуры

Процедура ТестДолжен_ВыполнитьАвторизацию_FTP() Экспорт
	
	Curl.Аутентификация("demo", "password")
		.Get("ftp://test.rebex.net");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(226);
	
КонецПроцедуры

Процедура ТестДолжен_ПопытатьсяВыполнитьАвторизациюСНевернымиДанными_FTP() Экспорт
	
	Curl.Аутентификация("curl", "password")
		.Get("ftp://test.rebex.net");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(530);
	
КонецПроцедуры

Процедура ТестДолжен_ПопытатьсяПолучитьНесуществующийФайлИВернутьСостояние550_FTP() Экспорт
	
	Curl.Get("ftp://demo:password@test.rebex.net/readme_.txt");
	
	Ожидаем.Что(Curl.КодСостояния()).Равно(550);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьНаличиеВКомандеДобавленнойПользовательскойОпции() Экспорт
	
	Curl.ДобавитьОпцию("--limit-rate", "1000");
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Содержит("--limit-rate ""1000""");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеУдаленнойПользовательскойОпции() Экспорт
	
	Опция = "--limit-rate";
	
	Curl.ДобавитьОпцию(Опция, "1000");
	Curl.УдалитьОпцию(Опция);
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьОтсутствиеВКомандеОчищенныхПользовательскихОпций() Экспорт
	
	Curl.ДобавитьОпцию("--limit-rate", "1000");
	Curl.ОчиститьОпции();
	
	Команда = Curl.СобратьКоманду("http://example.com/");
	
	Ожидаем.Что(Команда).Не_().Содержит("--limit-rate");
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_АутентификацияAWS3() Экспорт
	
	КлючДоступа = "access_key";
	СекретныйКлюч = "secret_key";
	Регион = "eu-west-1";
	Сервис = "s3";
	
	Эталон1 = СтрШаблон("--aws-sigv4 ""aws:amz:%1:%2""", Регион, Сервис);
	Эталон2 = СтрШаблон("--user ""%1:%2""", КлючДоступа, СекретныйКлюч);

	Curl.АутентификацияAWS4(КлючДоступа, СекретныйКлюч, Регион, Сервис);
	СтрокаКоманды = Curl.СобратьКоманду("https://amazonaws.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_СертификатКлиента() Экспорт
	
	ИмяФайлаСертификата = "certfile";   
	Пароль = "password";  
	
	Эталон = СтрШаблон("--cert ""%1:%2""", ИмяФайлаСертификата, Пароль);

	Curl.УстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_СертификатКлиентаСТипом() Экспорт
	
	ИмяФайлаСертификата = "certfile";
	Пароль = "password";
	ТипСертификата = "PEM";
	
	Эталон1 = СтрШаблон("--cert ""%1:%2""", ИмяФайлаСертификата, Пароль);
	Эталон2 = СтрШаблон("--cert-type ""%1""", ТипСертификата);

	Curl.УстановитьСертификатКлиента(ИмяФайлаСертификата, Пароль, ТипСертификата);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_ЗакрытыйКлючСертификата() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	
	Эталон = СтрШаблон("--key ""%1""", ИмяФайлаЗакрытогоКлюча);

	Curl.УстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_ЗакрытыйКлючСертификатаСТипом() Экспорт
	
	ИмяФайлаЗакрытогоКлюча = "keyfile";  
	ТипЗакрытогоКлюча = "PEM";
	
	Эталон1 = СтрШаблон("--key ""%1""", ИмяФайлаЗакрытогоКлюча);
	Эталон2 = СтрШаблон("--key-type ""%1""", ТипЗакрытогоКлюча);

	Curl.УстановитьЗакрытыйКлючСертификата(ИмяФайлаЗакрытогоКлюча, ТипЗакрытогоКлюча);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон1);
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон2);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_ИспользованиеСертификатовУдостоверяющихЦентровИзОС() Экспорт
	
	Опция = "--ca-native";
	
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Опция);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_СертификатыУдостоверяющихЦентров() Экспорт
	
	ИмяФайла = "CA-file.txt";

	Эталон = СтрШаблон("--cacert ""%1""", ИмяФайла);
	
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Содержит(Эталон);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_НаОтсутствиеИспользованияСертификатовУдостоверяющихЦентровИзОС() Экспорт
	
	ИмяФайла = "CA-file.txt";
	ОпцияСертификатыУЦИзОС = "--ca-native";
	
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(ОпцияСертификатыУЦИзОС);
	
КонецПроцедуры

Процедура ТестДолжен_ПроверитьСтрокуКоманды_НаОтсутствиеСертификатовУдостоверяющихЦентровИзФайла() Экспорт
	
	ИмяФайла = "CA-file.txt";
	ОпцияСертификатыУЦИзФайла = "--cacert";
	
	Curl.УстановитьСертификатыУдостоверяющихЦентров(ИмяФайла);
	Curl.ИспользоватьСертификатыУдостоверяющихЦентровИзОС();
	
	СтрокаКоманды = Curl.СобратьКоманду("https://example.com");	
	
	Ожидаем.Что(СтрокаКоманды).Не_().Содержит(ОпцияСертификатыУЦИзФайла);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиИПодключитьCURL()
	
	Файл = Новый Файл(ИспользуемоеИмяФайла);
	ПутьККаталогу = Файл.Путь;
	
	Файлы = НайтиФайлы(ПутьККаталогу, "cURL.epf");
	Если Файлы.Количество() = 0 Тогда
		ВызватьИсключение "Не найден файл cURL.epf в каталоге с тестами";
	КонецЕсли;
	
	ВнешниеОбработки.Создать(Файлы[0].ПолноеИмя, Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли